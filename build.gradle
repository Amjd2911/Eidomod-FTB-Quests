buildscript {
	repositories {
		jcenter()
		maven { url "https://files.minecraftforge.net/maven" }
		maven { url "https://plugins.gradle.org/m2/" }
		maven { url "https://dist.creeper.host/Sponge/maven" }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
		classpath 'com.wynprice.cursemaven:CurseMaven:2.+'
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'
apply plugin: 'com.wynprice.cursemaven'
apply plugin: 'org.spongepowered.mixin'

apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'

def ENV = System.getenv()
version = ENV.BUILD_NUMBER ? "${mod_version}.${ENV.BUILD_NUMBER}" : "${mod_version}.custom"
group = project.mod_package
archivesBaseName = project.mod_name

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	mappings channel: 'snapshot', version: project.mapping_version
	// accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	runs {
		client {
			workingDirectory project.file('run')
			args '--mixin.config', 'ftbquests.mixins.json'
			property 'forge.logging.console.level', 'debug'
			property 'mixin.env.disableRefMap', 'true'

			if (project.hasProperty('mc_dev_uuid')) args '--uuid', project.mc_dev_uuid
			if (project.hasProperty('mc_dev_username')) args '--username', project.mc_dev_username
			if (project.hasProperty('mc_dev_width')) args '--width', project.mc_dev_width
			if (project.hasProperty('mc_dev_height')) args '--height', project.mc_dev_height

			mods {
				ftbquests {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
			args '--mixin.config', 'ftbquests.mixins.json'
			property 'forge.logging.console.level', 'debug'

			mods {
				ftbquests {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')
			args '--mixin.config', 'ftbquests.mixins.json'
			property 'forge.logging.console.level', 'debug'

			args '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/')

			mods {
				ftbquests {
					source sourceSets.main
				}
			}
		}
	}
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
	mavenLocal()
	maven { url "https://maven.latmod.com/" } //FTB GUI Library, Item Filters, FTB Teams, KubeJS
}

dependencies {
	minecraft "net.minecraftforge:forge:${forge_version}"
	compile(fg.deobf(project.dependencies.create("com.feed_the_beast.mods:ftb-gui-library:${ftb_gui_library_version}") {
		transitive = false
		changing = true
	}))
	compile(fg.deobf(project.dependencies.create("dev.latvian.mods:item-filters:${itemfilters_version}") {
		transitive = false
		changing = true
	}))
	compile(fg.deobf(project.dependencies.create("com.feed_the_beast.mods:ftb-teams:${ftb_teams_version}") {
		transitive = false
		changing = true
	}))
	compile fg.deobf("curse.maven:rhino:${rhino_file}")
	compile fg.deobf("curse.maven:kubejs:${kubejs_file}")
	compile fg.deobf("curse.maven:jei:${jei_file}")
	compile fg.deobf("curse.maven:bookshelf:${bookshelf_file}")
	compile fg.deobf("curse.maven:game-stages:${gamestages_file}")
}

jar {
	manifest {
		attributes([
				"MixinConfigs"            : "ftbquests.mixins.json",
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
	type 'jar'
	builtBy 'reobfJar'
}

if (ENV.LOCAL_MAVEN) {
	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "file://${ENV.LOCAL_MAVEN}")
			}
		}
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = "release"
			addGameVersion "1.16.2"
			addGameVersion "1.16.3"
			addGameVersion "1.16.4"
			relations {
				requiredDependency 'ftb-gui-library'
				requiredDependency 'item-filters'
				optionalDependency 'kubejs'
				optionalDependency 'jei'
				optionalDependency 'packmode'
				optionalDependency 'game-stages'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

mixin {
	add sourceSets.main, "ftbquests.refmap.json"
}