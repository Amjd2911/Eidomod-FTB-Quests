plugins {
	id "com.github.johnrengelman.shadow" version "5.0.0"
	id "com.matthewprenger.cursegradle"
}

def ENV = System.getenv()

configurations {
	shadow
}

architectury {
	platformSetupLoomIde()
}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.architectury.minecraft}"
	mappings minecraft.officialMojangMappings()
	modCompile "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
	modCompile "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
	modCompile "me.shedaniel:architectury:${rootProject.architectury_version}:fabric"

	modCompile "curse.maven:ftb-teams-438497:${rootProject.ftb_teams_version_fabric}"
	modCompile "curse.maven:ftb-gui-library-438495:${rootProject.ftb_gui_library_version_fabric}"
	modCompile "curse.maven:item-filter-438500:${rootProject.itemfilters_version_fabric}"
	modCompile "curse.maven:kubejs-fabric-395864:${rootProject.kubejs_file_fabric}"
	modCompile "curse.maven:rhino-416294:${rhino_file}"

	compileOnly(project(path: ":common")) {
		transitive = false
	}
	runtimeOnly(project(path: ":common", configuration: "transformDevelopmentFabric")) {
		transitive = false
	}
	shadow(project(path: ":common", configuration: "transformProductionFabric")) {
		transitive = false
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	classifier "shadow"
}

remapJar {
	dependsOn(shadowJar)
	input.set(shadowJar.archivePath)
	classifier "fabric"
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id_fabric
			releaseType = "release"
			addGameVersion "Fabric"
			addGameVersion "1.16.4"
			addGameVersion "1.16.5"
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'architectury-fabric'
				requiredDependency 'fabric-api'
				optionalDependency 'kubejs-fabric'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}